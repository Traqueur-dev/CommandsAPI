plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'fr.traqueur'

allprojects {
    version = property('version')
    repositories {
        mavenCentral()
        maven {
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
    }
    def targetJavaVersion = 8
    java {
        def javaVersion = JavaVersion.toVersion(targetJavaVersion)
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        if (JavaVersion.current() < javaVersion) {
            toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
        }
    }
}

tasks.register('generateVersionProperties') {
    doLast {
        def file = new File("$projectDir/src/main/resources/version.properties")
        if (!file.parentFile.exists()) {
            file.parentFile.mkdirs()
        }
        file.text = "version=${project.version}"
    }
}

processResources.dependsOn generateVersionProperties

java {
    withSourcesJar()
    withJavadocJar()
}


publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

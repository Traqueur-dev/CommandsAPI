allprojects {
    group = 'fr.traqueur.commands'
    version = property('version')

    apply {
        plugin 'java-library'
    }

    repositories {
        mavenCentral()
        maven {
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }
    }
}

subprojects {
    if (!project.name.contains('test-plugin')) {

        dependencies {
            testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
            testImplementation 'org.mockito:mockito-core:5.3.1'
        }
        test {
            useJUnitPlatform()
            jvmArgs += ['-XX:+EnableDynamicAgentLoading']
            reports {
                html.required.set(true)
                junitXml.required.set(true)
            }

            testLogging {
                showStandardStreams = true
                events("passed", "skipped", "failed", "standardOut", "standardError")
                exceptionFormat "full"
            }
        }
    }
}

tasks.register("testAll") {
    group = "verification"
    description = "Execute tous les tests des sous-projets qui ont une tâche 'test'"

    // Déclare la dépendance vers chaque tâche 'test' de chaque sous-projet
    dependsOn subprojects
            .findAll { it.tasks.findByName('test') != null }
            .collect { it.tasks.named('test') }
}

plugins {
    id 'maven-publish'
    id("me.champeau.jmh") version "0.7.3"
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    jmh 'org.openjdk.jmh:jmh-core:1.37'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
}

def generatedResourcesDir = "$buildDir/generated-resources"

tasks.register('generateCommandsProperties') {
    doLast {
        def outputDir = file("$generatedResourcesDir")
        outputDir.mkdirs()
        def propsFile = new File(outputDir, "commands.properties")
        propsFile.text = "version=${project.version}"
    }
}

processResources.dependsOn(generateCommandsProperties)

// Inclure dans les ressources du build
sourceSets {
    main {
        resources {
            srcDir generatedResourcesDir
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'core'
            version = project.version
        }
    }
}